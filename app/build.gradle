plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

ext {
    mGroupId = "com.example.demoandroidapp"
    mArtifactId = "demo-android-app"
    mVersionCode = 1
    mVersionName = "1.4.0-beta.3"

    mLibraryName = "demo-android-app"
    mLibraryDescription = "demo android app test package"

}

android {
    namespace 'com.example.demoandroidapp'
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField("String", "SDK_NAME", "\"" + project.ext.mArtifactId + "\"")
            buildConfigField("String", "SDK_VERSION", "\"" + project.ext.mVersionName + "\"")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.1'
    implementation 'com.skyflowapi.android:skyflow-android-sdk:1.18.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // define a BOM and its version
    implementation(platform("com.squareup.okhttp3:okhttp-bom:4.10.0"))

    // define any required OkHttp artifacts without version
    implementation("com.squareup.okhttp3:okhttp")
    implementation("com.squareup.okhttp3:logging-interceptor")

    testImplementation("com.squareup.okhttp3:mockwebserver:4.10.0")
}


def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

def isBeta = project.hasProperty('beta') ? project.getProperty('beta') : false

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {

        publications {
            if (!isBeta) {
                maven(MavenPublication) {
                    groupId mGroupId
                    artifactId mArtifactId
                    version mVersionName

                    from components.release

                    artifact androidSourcesJar

                    pom {
                        name = mLibraryName
                        description = mLibraryDescription
                    }
                }
            } else {
                mavenBeta(MavenPublication) {
                    groupId mGroupId
                    artifactId "$mArtifactId-beta"
                    version mVersionName

                    from components.release

                    artifact androidSourcesJar

                    pom {
                        name = mLibraryName
                        description = mLibraryDescription
                    }
                }
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/skyflow-vivek/demoandroidapp")
                credentials {
                    username = localProperties.getProperty("gpr.user") ?: System.getenv("USERNAME")
                    password = localProperties.getProperty("gpr.key") ?: System.getenv("TOKEN")
                }
            }
        }
    }
}

publish.dependsOn assemble
